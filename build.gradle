buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.0'
    }
}

repositories {
    mavenCentral()
    maven {
        // this is a good maven repository with a lot of useful libraries.
        url 'https://github.com/Goddchen/mvn-repo/raw/master/'
    }
}

apply plugin: 'android'

// extend the runtime
configurations {
    unitTestCompile.extendsFrom runtime
    unitTestRuntime.extendsFrom unitTestCompile
}


dependencies {
    // HTTP library.
    compile 'com.android:volley:1.0'

    // REST library.
    compile 'com.squareup.retrofit:retrofit:1.4.1'

    // Notification library alternative to Toast
    compile 'de.keyboardsurfer.android.widget:crouton:1.8.3'

    androidTestCompile('com.jakewharton.espresso:espresso:1.1-r2') {

        // transient dependency to dagger is necessary as espresso will throw a
        // ClassNotFoundException.

        // exclude group: 'com.squareup.dagger'
    }

    // unitTestCompile 'junit:junit:4.11'

    unitTestCompile files("$project.buildDir/classes/release")
    unitTestCompile 'junit:junit:4.11'
    unitTestCompile 'org.robolectric:robolectric:2.1.+'
    unitTestCompile 'com.google.android:android:4.0.1.2'
}

// add a new unitTest sourceSet
sourceSets {
    unitTest {
        java.srcDir file('src/test/java')
        resources.srcDir file('src/test/resources')
    }
}


// add the unitTest task
task unitTest(type:Test, dependsOn: assemble) {
    description = "run unit tests"
    testClassesDir = project.sourceSets.unitTest.output.classesDir
    classpath = project.sourceSets.unitTest.runtimeClasspath
}

// bind to check
check.dependsOn unitTest

android {

    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19

        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
    }

    // this is necessary to get the test running, for more info see:
    // http://code.google.com/p/android-test-kit/wiki/Espresso
    packagingOptions {
        exclude 'LICENSE.txt'
    }

}
